yixxas

high

# `primaryCredit` and `secondaryCredit` are assumed to be 18 decimals in the calculations used but it is not the case

## Summary
The calculations used by the protocol, such as in `isSafe()` and `requestLiquidation` assumes that `state.primaryCredit` and `state.secondaryCredit` is 18 decimals, but they are in fact the decimals of the underlying assets used. In our case, they are USDC and JUSD respectively, hence both values are 6 decimals

## Vulnerability Detail

We first note that when we deposit, the line `state.primaryCredit[to] += SafeCast.toInt256(primaryAmount)` tells us that the value saved to `state.primaryCredit` is `primaryAmount`, which uses the same decimals as the underlying asset, hence we can be certain that decimals of `primaryCredit` is 6 decimals. This is also true for `secondaryCredit`.

```solidity
    function deposit(
        Types.State storage state,
        uint256 primaryAmount,
        uint256 secondaryAmount,
        address to
    ) external {
        if (primaryAmount > 0) {
            IERC20(state.primaryAsset).safeTransferFrom(
                msg.sender,
                address(this),
                primaryAmount
            );
            state.primaryCredit[to] += SafeCast.toInt256(primaryAmount);
        }
        if (secondaryAmount > 0) {
            IERC20(state.secondaryAsset).safeTransferFrom(
                msg.sender,
                address(this),
                secondaryAmount
            );
            state.secondaryCredit[to] += secondaryAmount;
        }
        emit Deposit(to, msg.sender, primaryAmount, secondaryAmount);
    }
```

Next, in `getTotalExposure`, we see these calculations. Decimal math is used hence we know `netPositionValue` is 18 decimals. 

```solidity
netPositionValue += paperAmount.decimalMul(price) + creditAmount;
uint256 exposureIncrement = paperAmount.decimalMul(price).abs();
exposure += exposureIncrement;
maintenanceMargin +=
	(exposureIncrement * params.liquidationThreshold) /
	Types.ONE;
```

This value is then passed into `_isSafe()` and added with `primaryCredit`, but they are of different decimals as we have previously established.

```solidity
function _isSafe(Types.State storage state, address trader)
	internal
	view
	returns (bool)
{
	(
		int256 netPositionValue,
		,
		uint256 maintenanceMargin
	) = getTotalExposure(state, trader);

	// net value >= maintenanceMargin
	return
		netPositionValue +
			state.primaryCredit[trader] +
			SafeCast.toInt256(state.secondaryCredit[trader]) >=
		SafeCast.toInt256(maintenanceMargin);
}
```

The same is true for `requestLiquidation()`.

## Impact
Values of different decimals added to each other means wrong accounting of the protocol for important values.

## Code Snippet
https://github.com/sherlock-audit/2023-04-jojo/blob/main/smart-contract-EVM/contracts/lib/Funding.sol#L57-L79
https://github.com/sherlock-audit/2023-04-jojo/blob/main/smart-contract-EVM/contracts/lib/Liquidation.sol#L87-L104

## Tool used

Manual Review

## Recommendation
Consider converting `primaryCredit` and `secondaryCredit` to 18 decimals when we first deposit them.

