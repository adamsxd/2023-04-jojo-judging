cccz

medium

# When updating the funding rate for the first time, the maxChange will be very large

## Summary
When updating the funding rate for the first time, the maxChange will be very large
## Vulnerability Detail
When updateFundingRate is called to update the funding rate, maxChange is used to control the change of the funding rate
```solidity
    function updateFundingRate(
        address[] calldata perpList,
        int256[] calldata rateList
    ) external onlyOwner {
        for (uint256 i = 0; i < perpList.length;) {
            address perp = perpList[i];
            int256 oldRate = IPerpetual(perp).getFundingRate();
            uint256 maxChange = getMaxChange(perp);
            require(
                (rateList[i] - oldRate).abs() <= maxChange,
                "FUNDING_RATE_CHANGE_TOO_MUCH"
            );
            fundingRateUpdateTimestamp[perp] = block.timestamp;
            unchecked {
                ++i;
            }
        }

        IDealer(dealer).updateFundingRate(perpList, rateList);
    }
```
In getMaxChange, the timeInterval determines the maxChange, and since the fundingRateUpdateTimestamp = 0 when updateFundingRate is first called, this results in a very large timeInterval and maxChange
```solidity
    function getMaxChange(address perp) public view returns (uint256) {
        Types.RiskParams memory params = IDealer(dealer).getRiskParams(perp);
        uint256 markPrice = IMarkPriceSource(params.markPriceSource)
            .getMarkPrice();
        uint256 timeInterval = block.timestamp -
            fundingRateUpdateTimestamp[perp];
        uint256 maxChangeRate = (speedMultiplier *
            timeInterval *
            params.liquidationThreshold) / (1 days);
        uint256 maxChange = (maxChangeRate * markPrice) / Types.ONE;
        return maxChange;
    }
```
## Impact
This results in a loss of control of the change of the funding rate in updateFundingRate.
## Code Snippet
https://github.com/sherlock-audit/2023-04-jojo/blob/main/smart-contract-EVM/contracts/fundingRateKeeper/FundingRateUpdateLimiter.sol#L60-L71

## Tool used

Manual Review

## Recommendation
Change to
```diff
    function getMaxChange(address perp) public view returns (uint256) {
        Types.RiskParams memory params = IDealer(dealer).getRiskParams(perp);
        uint256 markPrice = IMarkPriceSource(params.markPriceSource)
            .getMarkPrice();
-       uint256 timeInterval = block.timestamp -
-           fundingRateUpdateTimestamp[perp];
+       uint256 timeInterval = fundingRateUpdateTimestamp[perp] == 0 ? 8 hours : block.timestamp -
+           fundingRateUpdateTimestamp[perp];
```