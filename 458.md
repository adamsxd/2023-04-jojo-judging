ArbitraryExecution

high

# Assets in `FlashLoanLiquidate`, `FlashLoanRepay`, and `GeneralRepay` can be stolen

## Summary

The `FlashLoanLiquidate`, `FlashLoanRepay`, and `GeneralRepay` contracts expose external functions that allow any user to call those functions with arbitrary calldata to an arbitrary address.

## Vulnerability Detail

The `external` [`repayJUSD`](https://github.com/JOJOexchange/JUSDV1/blob/011e10d36257a404c8c1d7d2b8c9f01a2b7a1969/src/Impl/flashloanImpl/GeneralRepay.sol#L32) function in `GeneralRepay.sol` is intended for users to repay any borrowed `JUSD` tokens. When `repayJUSD` is called, users have full control of the input, including the value that is to be used in the [`param`](https://github.com/JOJOexchange/JUSDV1/blob/011e10d36257a404c8c1d7d2b8c9f01a2b7a1969/src/Impl/flashloanImpl/GeneralRepay.sol#L36) argument. As long as the `asset != USDC` check is satisfied, the function will [decode](https://github.com/JOJOexchange/JUSDV1/blob/011e10d36257a404c8c1d7d2b8c9f01a2b7a1969/src/Impl/flashloanImpl/GeneralRepay.sol#L42) the `param` value passed in from the user. The values decoded from `param`, specifically `swapTarget` and `data` will be used in the external call on [line 44](https://github.com/JOJOexchange/JUSDV1/blob/011e10d36257a404c8c1d7d2b8c9f01a2b7a1969/src/Impl/flashloanImpl/GeneralRepay.sol#L44) of `GeneralRepay.sol`.

Because the `swapTarget` and `data` values are both controlled by the user calling the `repayJUSD` function, a user can encode any arbitrary calldata to be called on any arbitrary address. An attacker could use this external call to perform a `transfer` for any tokens held in the `GeneralRepay` contract to an address of their choosing, as well as a `transferFrom` call to drain any approved allowances of tokens approved to the `GeneralRepay` contract.

Similarly, the external [`JOJOFlashLoan`](https://github.com/JOJOexchange/JUSDV1/blob/011e10d36257a404c8c1d7d2b8c9f01a2b7a1969/src/Impl/flashloanImpl/FlashLoanRepay.sol#L35) function in `FlashLoanRepay.sol` and the external [`JOJOFlashLoan`](https://github.com/JOJOexchange/JUSDV1/blob/011e10d36257a404c8c1d7d2b8c9f01a2b7a1969/src/Impl/flashloanImpl/FlashLoanLiquidate.sol#L46) function in `FlashLoanLiquidate.sol` are susceptible to the same attack vector.

## Impact

The impact of this vulnerability is that any tokens held in the `FlashLoanLiquidate`, `FlashLoanRepay`, and `GeneralRepay` contracts, as well as any approved allowances that those contracts have for tokens can be stolen. Assuming that tokens adhere to ERC20 or ERC721 standards, an attacker could use the external calls to pass in encoded calldata to invoke the `transfer` or `transferFrom` methods on ERC20 or ERC721 contracts to transfer tokens to the attacker's address.

## Code Snippet

[`GeneralRepay.sol`](https://github.com/JOJOexchange/JUSDV1/blob/011e10d36257a404c8c1d7d2b8c9f01a2b7a1969/src/Impl/flashloanImpl/GeneralRepay.sol#L32-L54C10):

```solidity
function repayJUSD(
    address asset,
    uint256 amount,
    address to,
    bytes memory param
) external {
    IERC20(asset).safeTransferFrom(msg.sender, address(this), amount);
    uint256 minReceive;
    if (asset != USDC) {
        (address approveTarget, address swapTarget, uint256 minAmount, bytes memory data) = abi
            .decode(param, (address, address, uint256, bytes));
        IERC20(asset).approve(approveTarget, amount);
        (bool success, ) = swapTarget.call(data);
        if (success == false) {
            assembly {
                let ptr := mload(0x40)
                let size := returndatasize()
                returndatacopy(ptr, 0, size)
                revert(ptr, size)
            }
        }
        minReceive = minAmount;
    }
```

[`FlashLoanRepay.sol`](https://github.com/JOJOexchange/JUSDV1/blob/011e10d36257a404c8c1d7d2b8c9f01a2b7a1969/src/Impl/flashloanImpl/FlashLoanRepay.sol#L35-L52):

```solidity
function JOJOFlashLoan(
    address asset,
    uint256 amount,
    address to,
    bytes calldata param
) external {
    (address approveTarget, address swapTarget, uint256 minReceive, bytes memory data) = abi
        .decode(param, (address, address, uint256, bytes));
    IERC20(asset).approve(approveTarget, amount);
    (bool success, ) = swapTarget.call(data);
    if (success == false) {
        assembly {
            let ptr := mload(0x40)
            let size := returndatasize()
            returndatacopy(ptr, 0, size)
            revert(ptr, size)
        }
    }
```

[`FlashLoanLiquidate.sol`](https://github.com/JOJOexchange/JUSDV1/blob/011e10d36257a404c8c1d7d2b8c9f01a2b7a1969/src/Impl/flashloanImpl/FlashLoanLiquidate.sol#L46-L70):

```solidity
function JOJOFlashLoan(
    address asset,
    uint256 amount,
    address to,
    bytes calldata param
) external {
    //swapContract swap
    (LiquidateData memory liquidateData, bytes memory originParam) = abi
        .decode(param, (LiquidateData, bytes));
    (
        address approveTarget,
        address swapTarget,
        address liquidator,
        bytes memory data
    ) = abi.decode(originParam, (address, address, address, bytes));
    IERC20(asset).approve(approveTarget, amount);
    (bool success, ) = swapTarget.call(data);
    if (success == false) {
        assembly {
            let ptr := mload(0x40)
            let size := returndatasize()
            returndatacopy(ptr, 0, size)
            revert(ptr, size)
        }
    }
```

## Tool used

Manual review.

## Recommendation

Consider an implementation where the user is not allowed to have full control over the `param` data that is passed into the function calls. Alternatively, consider checking first if the `swapTarget` address is a token address and then secondly, checking if the balance and allowance balances of the `FlashLoanLiquidate`, `FlashLoanRepay`, and `GeneralRepay` contracts for the `swapTarget` token address have not changed after the function calls.
