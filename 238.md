branch_indigo

medium

# No check on Oracle Zero Price Reporting Resulting in Imminent Liquidations

## Summary
There are some extreme cases where Chainlink might take oracles offline or the oracle may falsely returns a zero price. In such cases, the oracle price from JOJOOracleAdaptor.sol will not be checked or handled, and may cause collaterals to be falsely valued at zero, leading to imminent liquidations.
## Vulnerability Detail
A zero price reporting is possible even though uncommon. Chainlink has taken the oracle offline before (Luna Crash) resulting a price reporting different from market price. In a case like this, zero price reporting shouldn't be valid. In other cases, (near) zero price might be the actual market price if the token simply crashes, in which case zero price reporting is considered valid. 

Given the probability of both scenarios, oracle price reporting should be checked and verified when reaching an extreme value such as zero. 

But we see there is no handling of oracle return values in JOJOOracleAdaptor.sol and JUSDView.sol.In JOJOOracleAdaptor.sol `getAssetPrice()`, only data freshness is checked and a zero oracle price value will return zero in tokenPrice. And in JUSDView.sol `getMintAmount()` and `getMintAmountBorrow()`, these prices are directly multiplied by collateral amount which return zero. In `_isStartLiquidation`, zero value is directly passed to liquidationMaxMintAmount likely result the account to be eligible for liquidation. In such case, the liquidation is quite profitable for liquidators since they are likely be able to sell the collaterals at the real market price that is above zero. And the liquidated account is very likely losing all collaterals including tokens with accurately reported prices. 

Although there are ways of manual intervening with emergency oracles, such measures might not be prompt to prevent losses in time. And the losses would be quite extensive affecting most users holding the collateral token.

## Impact
Users with inaccurately reported collateral tokens are very likely to be liquidated losing all collaterals. 
## Code Snippet
[https://github.com/JOJOexchange/JUSDV1/blob/011e10d36257a404c8c1d7d2b8c9f01a2b7a1969/src/oracle/JOJOOracleAdaptor.sol#L26-L35](https://github.com/JOJOexchange/JUSDV1/blob/011e10d36257a404c8c1d7d2b8c9f01a2b7a1969/src/oracle/JOJOOracleAdaptor.sol#L26-L35)
```solidity
    function getAssetPrice() external view override returns (uint256) {
        /*uint80 roundID*/
        (, int256 price,, uint256 updatedAt,) = IChainLinkAggregator(chainlink).latestRoundData();
        (, int256 USDCPrice,, uint256 USDCUpdatedAt,) = IChainLinkAggregator(USDCSource).latestRoundData();

        require(block.timestamp - updatedAt <= heartbeatInterval, "ORACLE_HEARTBEAT_FAILED");
        require(block.timestamp - USDCUpdatedAt <= heartbeatInterval, "USDC_ORACLE_HEARTBEAT_FAILED");
        uint256 tokenPrice = (SafeCast.toUint256(price) * 1e8) / SafeCast.toUint256(USDCPrice);
        return tokenPrice * JOJOConstant.ONE / decimalsCorrection;
    }
}
```
[https://github.com/JOJOexchange/JUSDV1/blob/011e10d36257a404c8c1d7d2b8c9f01a2b7a1969/src/Impl/JUSDView.sol#L94-L104](https://github.com/JOJOexchange/JUSDV1/blob/011e10d36257a404c8c1d7d2b8c9f01a2b7a1969/src/Impl/JUSDView.sol#L94-L104)
```solidity
    function _getMintAmount(
        uint256 balance,
        address oracle,
        uint256 rate
    ) internal view returns (uint256) {
        return
            IPriceChainLink(oracle)
                .getAssetPrice()
                .decimalMul(balance)
                .decimalMul(rate);
    }
```
[https://github.com/JOJOexchange/JUSDV1/blob/011e10d36257a404c8c1d7d2b8c9f01a2b7a1969/src/Impl/JUSDView.sol#L106-L118](https://github.com/JOJOexchange/JUSDV1/blob/011e10d36257a404c8c1d7d2b8c9f01a2b7a1969/src/Impl/JUSDView.sol#L106-L118)
```solidity
    function _getMintAmountBorrow(
        DataTypes.ReserveInfo memory reserve,
        uint256 amount
    ) internal view returns (uint256) {
        uint256 depositAmount = IPriceChainLink(reserve.oracle)
            .getAssetPrice()
            .decimalMul(amount)
            .decimalMul(reserve.initialMortgageRate);
        if (depositAmount >= reserve.maxColBorrowPerAccount) {
            depositAmount = reserve.maxColBorrowPerAccount;
        }
        return depositAmount;
    }
```
[https://github.com/JOJOexchange/JUSDV1/blob/011e10d36257a404c8c1d7d2b8c9f01a2b7a1969/src/Impl/JUSDView.sol#L197-L201](https://github.com/JOJOexchange/JUSDV1/blob/011e10d36257a404c8c1d7d2b8c9f01a2b7a1969/src/Impl/JUSDView.sol#L197-L201)
```solidity
            liquidationMaxMintAmount += _getMintAmount(
                liquidatedTraderInfo.depositBalance[collateral],
                reserve.oracle,
                reserve.liquidationMortgageRate
            );
```
## Tool used

Manual Review

## Recommendation
Add checks for zero price reporting against a secondary oracle to verify the validity of zero value price before calculating collaterals. If zero price is invalid, it shouldn't be used to directly. 
