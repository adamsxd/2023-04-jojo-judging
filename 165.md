Juntao

medium

# User can borrow JUSD and repay without interest

## Summary

User can borrow JUSD by depositing collateral, when repay, user need to pay principle and interest, however, it's possible that user can repay without interest.

## Vulnerability Detail

To repay a loan, the borrowed amount is first calculated at [L315](https://github.com/JOJOexchange/JUSDV1/blob/c04572261828c0feb30d6739e595ca02e0122873/src/Impl/JUSDBank.sol#L315):
```solidity
uint256 JUSDBorrowed = user.t0BorrowBalance.decimalMul(tRate);
```
JOJO uses a floating fee rate, which is equivalent to pulling back to time t0, calculating how much user borrowed at that time, and then multiplying it by the rate at the current time.
Let's examine the [**_decimalMul(uint256 a, uint256 b)_**](https://github.com/JOJOexchange/JUSDV1/blob/c04572261828c0feb30d6739e595ca02e0122873/src/lib/DecimalMath.sol#L10-L12) in DecimalMath.sol:
```solidity
function decimalMul(uint256 a, uint256 b) internal pure returns (uint256) {
    return (a * b) / ONE;
}
```
We can see that when the interest is rounded down instead of being rounded up, and some amount of interest will be ignored in calculation, which means user can get a free loan if he/she lend a small loan and repay in a short time. 

Let's say Alice deposited some collaterals and borrowed 120u JUSD at time t0 in block 1, and she decided to repay in block 2 which is 13 seconds later. By the time of repaying, the _t0BorrowBalance_ is _120000000_ and _tRate_ is _1000000008244545915_, so _JUSDBorrowed_ is  

> 120000000 * 1000000008244545915 / ONE

which equals to

> 120000000989345509800000000 / 1000000000000000000

The final result returns 120000000, some interest is ignored due to rounding down.

Please see the Test Codes:
```solidity
function testBorrowWithoutInterest() public {
    mockToken1.transfer(alice, 100e18);

    vm.startPrank(alice);

    // alice deposits collateral
    mockToken1.approve(address(jusdBank), 10e18);
    jusdBank.deposit(alice, address(mockToken1), 10e18, alice);

    // alice borrows 120u
    jusdBank.borrow(120e6, alice, false);
    uint256 borrowedAmount = jusdBank.getBorrowBalance(alice);
    assertEq(borrowedAmount, 120e6);
    assertEq(jusd.balanceOf(alice), 120e6);

    // jump to the next block (13 seconds)
    vm.warp(block.timestamp + 13);

    // no interest accrued
    borrowedAmount = jusdBank.getBorrowBalance(alice);
    assertEq(borrowedAmount, 120e6);

    // alice repay her debt without paying interest
    jusd.approve(address(jusdBank), type(uint256).max);
    jusdBank.repay(borrowedAmount, alice);
    borrowedAmount = jusdBank.getBorrowBalance(alice);
    assertEq(borrowedAmount, 0);
}
```

## Impact

The less user borrows, the longer "interest free period" he can enjoy, and if multiple accounts borrow and repay like this, the protocol may suffer much loss of interest.

## Code Snippet

https://github.com/JOJOexchange/JUSDV1/blob/c04572261828c0feb30d6739e595ca02e0122873/src/lib/DecimalMath.sol#L10-L12
https://github.com/JOJOexchange/JUSDV1/blob/c04572261828c0feb30d6739e595ca02e0122873/src/Impl/JUSDView.sol#L83-L85

## Tool used

Manual Review

## Recommendation

Please consider to modify as below:
1. In DecimalMath.sol
```solidity
function decimalMul(uint256 a, uint256 b) internal pure returns (uint256) {
    return (a * b) % ONE == 0 ? (a * b) / ONE : (a * b) / ONE + 1;
}
```
2. In JUSDView.sol
```solidity
function getBorrowBalance(address from) external view returns (uint256) {
    return (userInfo[from].t0BorrowBalance * getTRate()) % 1e18 == 0 ?
         (userInfo[from].t0BorrowBalance * getTRate()) / 1e18 :
         (userInfo[from].t0BorrowBalance * getTRate()) / 1e18 + 1;
}
```
