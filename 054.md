0xStalin

high

# Operators can steal client's collaterals - JUSD System

## Summary
An Operator can steal collateral from the Client's account by depositing it into an account owned by the operator itself 

## Vulnerability Detail
A client's operator is capable of doing deposits on behalf of its client.

- Once an operator is approved by its client after calling the setOperator():JUSDOperation.sol, all the client's funds could be stolen if the operator abuses the privilege of making deposits on behalf of its client
```solidity
function setOperator(address operator, bool isOperator) external {
    operatorRegistry[msg.sender][operator] = isOperator;
    emit SetOperator(msg.sender, operator, isOperator);
}
```

**When an operator makes a deposit, the collateral is taken from the client's account**, and _**the operator is capable to specify without any restrictions what address will be credited as the owner of that collateral inside the system!**_

```solidity
function deposit(
      address from,
      address collateral,
      uint256 amount,
      address to
  ) external override nonReentrant isValidOperator(msg.sender, from) {
      DataTypes.ReserveInfo storage reserve = reserveInfo[collateral];
      DataTypes.UserInfo storage user = userInfo[to];
      //        deposit
      _deposit(reserve, user, amount, collateral, to, from);
  }
```

- The isValidOperator() modifier only validates that the caller is a valid operator, but **it does not validate if the `to` address** (The address that will be credited as the owner of the deposited collateral) **is indeed an account owned by the client account from where the collateral will be taken**
```solidity
modifier isValidOperator(address operator, address client) {
    require(
        msg.sender == client || operatorRegistry[client][operator],
        JUSDErrors.CAN_NOT_OPERATE_ACCOUNT
    );
    _;
}
```

- https://github.com/sherlock-audit/2023-04-jojo/blob/main/JUSDV1/src/Impl/JUSDBank.sol#L102
- When the _deposit() is called to actually make the transferFrom the client's account (`from`) and credit the `to` address, notice that:

1. The parameter `user` points to the `userInfo[to]`, that means, all the changes made to the `user` parameter inside the _deposit() will end up updating the state of the `to` address, which it can be any address and not necessarily an address owned by the client!
2. The parameter `from` represents the client of the operator, from where the collateral will be taken out.

```solidity
    function _deposit(
        DataTypes.ReserveInfo storage reserve,
        DataTypes.UserInfo storage user,
        uint256 amount,
        address collateral,
        address to,
        address from
    ) internal {
        require(reserve.isDepositAllowed, JUSDErrors.RESERVE_NOT_ALLOW_DEPOSIT);
        require(amount != 0, JUSDErrors.DEPOSIT_AMOUNT_IS_ZERO);
        IERC20(collateral).safeTransferFrom(from, address(this), amount);
        _addCollateralIfNotExists(user, collateral);
        user.depositBalance[collateral] += amount;
        reserve.totalDepositAmount += amount;
        
       ....

    }
```
- On [line](https://github.com/sherlock-audit/2023-04-jojo/blob/main/JUSDV1/src/Impl/JUSDBank.sol#L257), the collateral is taken out from the client's address and send it to the JUSDBank contract.
- And on [line](https://github.com/sherlock-audit/2023-04-jojo/blob/main/JUSDV1/src/Impl/JUSDBank.sol#L259), **the amount of the collateral that was transferred from the client address is credited to** the `user` variable which **is pointing to the storage of `userInfo[to]`** 

- At this point, the `to` address has been credited as the owner of the newly deposited collateral, and now they can make use of that collateral as they please, they can either withdraw the collateral from the system or borrow JUSDs.

## Impact
Operators can fund accounts of their own by using the collateral of their clients, and then the funded accounts can withdraw that collateral from the system or use it to borrow JUSD.

## Code Snippet
The operator sets the `to` address to be an account owned by him, instead of an account owned by his client
https://github.com/sherlock-audit/2023-04-jojo/blob/main/JUSDV1/src/Impl/JUSDBank.sol#L93-L117

The _deposit() credits the newly deposited collateral to the `to` address, regarding if the account is not owned by the client
https://github.com/sherlock-audit/2023-04-jojo/blob/main/JUSDV1/src/Impl/JUSDBank.sol#L247-L271

## Tool used
Manual Review

## Recommendation
- A Potential fix could be to create a list that stores all the accounts that a client approves to deposit funds from its address, even if the transfer is requested by the operator, something like **`mapping(address => mapping(address => bool)) clientApprovedAddress`**, where the logic is something like  `client => addressReceivingFunds => true/false`

- And then add a modifier to the deposit() function that will check if the `to` address is an approvedAddress to be funded by the client

- The idea is that the client can have more granular control over what accounts can be funded with its own funds
- If the operator wants to add or remove an approved account from the list, first the operator proposes the change, and then the client/owner must accept the change
- In this way, the operators can't abuse their funding power by stealing collateral from their clients to fund accounts of their own, and the clients have control over what accounts can be funded with their own funds.



