nobody2018

medium

# The value of delisted collateral should not be treated as 0

## Summary

When a collateral is delisted, `JUSDView#_isStartLiquidation` will treat the value of the collateral as 0. **However, the real price of the delisted collateral isn't 0**. This will lead to the forced liquidation of borrowers with this collateral. This is obviously unfair.

## Vulnerability Detail

A collateral is delisted and its `reserve.isFinalLiquidation` is set to true. The liquidator can liquidate all users with this collateral. These users include users with only this collateral and users with multiple collaterals. Since the price of the delisted collateral is treated as 0, the collateral of **users who should not be triggered for liquidation** will be sold to the liquidator at a discount. In the `JUSDBank#liquidate`Â  process, `JUSDView#_isStartLiquidation` is used to check whether an account can be liquidated. This function will get the real value of the specified user's all the collaterals to check whether total vaule is lower than the user's debt.

```solidity
function _isStartLiquidation(
        DataTypes.UserInfo storage liquidatedTraderInfo,
        uint256 tRate
    ) internal view returns (bool) {
        uint256 JUSDBorrow = (liquidatedTraderInfo.t0BorrowBalance).decimalMul(
            tRate
        );
        uint256 liquidationMaxMintAmount;
        address[] memory collaterals = liquidatedTraderInfo.collateralList;
        for (uint256 i; i < collaterals.length; i = i + 1) {
            address collateral = collaterals[i];
            DataTypes.ReserveInfo memory reserve = reserveInfo[collateral];
->          if (reserve.isFinalLiquidation) {	//when collateral is delisted, if statement will be met.
                continue;
            }
            liquidationMaxMintAmount += _getMintAmount(
                liquidatedTraderInfo.depositBalance[collateral],
                reserve.oracle,
                reserve.liquidationMortgageRate
            );
        }
->      return liquidationMaxMintAmount < JUSDBorrow;
    }
```

In fact, the delisted collateral has a market price, and users will absolutely not accept its price as 0.

## Impact

Users who should not be liquidated are liquidated and suffer a loss of funds.

## Code Snippet

https://github.com/sherlock-audit/2023-04-jojo/blob/main/JUSDV1/src/Impl/JUSDBank.sol#L166-L170

https://github.com/sherlock-audit/2023-04-jojo/blob/main/JUSDV1/src/Impl/JUSDBank.sol#L391-L394

https://github.com/sherlock-audit/2023-04-jojo/blob/main/JUSDV1/src/Impl/JUSDView.sol#L182-L204

## Tool used

Manual Review

## Recommendation

When a collateral is delisted, users cannot deposit the collateral, nor can they use the collateral to borrow JUSD. However, the price of this collateral should not be treated as zero during the liquidation process.

```diff
diff --git a/JUSDV1/src/Impl/JUSDBank.sol b/JUSDV1/src/Impl/JUSDBank.sol
index 7d76756..2681b58 100644
--- a/JUSDV1/src/Impl/JUSDBank.sol
+++ b/JUSDV1/src/Impl/JUSDBank.sol
@@ -389,7 +389,7 @@ contract JUSDBank is IJUSDBank, JUSDOperation, JUSDView, JUSDMulticall {
         );
         uint256 tRate = getTRate();
         require(
-            _isStartLiquidation(liquidatedInfo, tRate),
+            _isStartLiquidation(liquidatedInfo, tRate, true),
             JUSDErrors.ACCOUNT_IS_SAFE
         );
         DataTypes.ReserveInfo memory reserve = reserveInfo[collateral];
@@ -500,7 +500,7 @@ contract JUSDBank is IJUSDBank, JUSDOperation, JUSDView, JUSDMulticall {
         uint256 tRate = getTRate();
         if (
             liquidatedTraderInfo.collateralList.length == 0 &&
-            _isStartLiquidation(liquidatedTraderInfo, tRate)
+            _isStartLiquidation(liquidatedTraderInfo, tRate, false)
         ) {
             DataTypes.UserInfo storage insuranceInfo = userInfo[insurance];
             uint256 borrowJUSDT0 = liquidatedTraderInfo.t0BorrowBalance;
diff --git a/JUSDV1/src/Impl/JUSDView.sol b/JUSDV1/src/Impl/JUSDView.sol
index 6059858..7f9e35e 100644
--- a/JUSDV1/src/Impl/JUSDView.sol
+++ b/JUSDV1/src/Impl/JUSDView.sol
@@ -57,7 +57,7 @@ abstract contract JUSDView is JUSDBankStorage, IJUSDBank {

     function isAccountSafe(address user) external view returns (bool) {
         DataTypes.UserInfo storage userInfo = userInfo[user];
-        return !_isStartLiquidation(userInfo, getTRate());
+        return !_isStartLiquidation(userInfo, getTRate(), false);
     }

     function getCollateralPrice(
@@ -181,7 +181,8 @@ abstract contract JUSDView is JUSDBankStorage, IJUSDBank {
     // liquidationMaxMintAmount = sum(depositAmount * price * liquidationMortgageRate)
     function _isStartLiquidation(
         DataTypes.UserInfo storage liquidatedTraderInfo,
-        uint256 tRate
+        uint256 tRate,
+        bool forceGetPrice
     ) internal view returns (bool) {
         uint256 JUSDBorrow = (liquidatedTraderInfo.t0BorrowBalance).decimalMul(
             tRate
@@ -191,7 +192,7 @@ abstract contract JUSDView is JUSDBankStorage, IJUSDBank {
         for (uint256 i; i < collaterals.length; i = i + 1) {
             address collateral = collaterals[i];
             DataTypes.ReserveInfo memory reserve = reserveInfo[collateral];
-            if (reserve.isFinalLiquidation) {
+            if (!forceGetPrice && reserve.isFinalLiquidation) {
                 continue;
             }
             liquidationMaxMintAmount += _getMintAmount(
```